plugins {
    id 'fabric-loom'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

version = minecraft_version + "-" + mod_version
group = "me.zeroeightsix"

minecraft {}

repositories {
    jcenter()
    mavenCentral()
    maven {
        name = "jitpack.io"
        url = "https://jitpack.io"
    }
    maven {
        name = 'impactdevelopment-repo'
        url = 'https://impactdevelopment.github.io/maven/'
    }
    maven {
        name = "modmuss50's repo"
        url = "https://maven.modmuss50.me/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

//    compile "me.zeroeightsix:fiber:${project.fiber_version}-SNAPSHOT"
    implementation 'com.github.fablabsmc:fiber:fix~undetected-mutation-SNAPSHOT'

    compile "com.github.ZeroMemes:Alpine:1.9"
    compile group: 'net.jodah', name: 'typetools', version: '0.5.0'
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.19'
    compile(group: 'org.reflections', name: 'reflections', version: '0.9.11') {
        exclude group: 'com.google.guava', module: 'guava'
    }

    compile 'com.github.kotlin-graphics:imgui:jdk8-SNAPSHOT'
    compile 'com.github.kotlin-graphics:glm:jdk8-SNAPSHOT'
    compile 'com.github.kotlin-graphics:uno-sdk:jdk8-SNAPSHOT'
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.3.61'
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// extra kami tasks

task mkdir {
    group = "kami"
    doLast {
        mkdir System.getProperty("user.home") + "/.minecraft/mods/"
    }
}

task rmOld(type: Delete) {
    group = "kami"
    delete fileTree(System.getProperty("user.home") + "/.minecraft/mods/").matching {
        include "KAMI*-release.jar"
    }
}

task copy(type: Copy) {
    group = "kami"
    from file("build/libs/")
    include 'KAMI*-release.jar'
    into file(System.getProperty("user.home") + "/.minecraft/mods/")
}

compileKotlin.kotlinOptions.jvmTarget = "1.8"